block(`IpAccessList_rules) {
   alias_all(`libbatfish:Flow),
   alias_all(`libbatfish:Ip),
   alias_all(`libbatfish:IpAccessList),
   clauses(`{
// BEGIN CODE

IpAccessListDeny(list, line, flow) <-
   SetIpAccessListLine_deny(list, line),
   IpAccessListFirstMatch[list, flow] = line.

IpAccessListPermit(list, line, flow) <-
   SetIpAccessListLine_permit(list, line),
   IpAccessListFirstMatch[list, flow] = line.

IpAccessListFirstMatch[list, flow] = firstMatchLine <-
   agg<<firstMatchLine = min(line)>>
      IpAccessListMatch(list, line, flow).

IpAccessListLine(list, line) <-
   SetIpAccessListLine_deny(list, line) ;
   SetIpAccessListLine_permit(list, line).

IpAccessListMatch(list, line, flow) <-
   IpAccessListMatchDstIp(list, line, flow),
   IpAccessListMatchDstPort(list, line, flow),
   IpAccessListMatchProtocol(list, line, flow),
   IpAccessListMatchSrcIp(list, line, flow),
   IpAccessListMatchSrcPort(list, line, flow).

IpAccessListMatchDstIp(list, line, flow) <-
   IpAccessListLine(list, line),
   Flow_dstIp[flow] = dstIp,
   (
      !SetIpAccessListLine_dstIpRange(list, line, _, _) ;
      (
         SetIpAccessListLine_dstIpRange(list, line, dstIp_start, dstIp_end),
         Ip_address(dstIp:dstIpNum),
         dstIp_start <= dstIpNum <= dstIp_end
      )
   ).

IpAccessListMatchDstPort(list, line, flow) <-
   IpAccessListLine(list, line),
   Flow_dstPort[flow] = dstPort,
   (
      !SetIpAccessListLine_dstPortRange(list, line, _, _) ;
      (
         SetIpAccessListLine_dstPortRange(list, line, dstPort_start, dstPort_end),
         dstPort_start <= dstPort <= dstPort_end
      )
   ).

IpAccessListMatchProtocol(list, line, flow) <-
   IpAccessListLine(list, line),
   Flow_ipProtocol[flow] = protocol,
   (
      !SetIpAccessListLine_protocol(list, line, _) ;
      SetIpAccessListLine_protocol(list, line, protocol)
   ).

IpAccessListMatchSrcIp(list, line, flow) <-
   IpAccessListLine(list, line),
   Flow_srcIp[flow] = srcIp,
   (
      !SetIpAccessListLine_srcIpRange(list, line, _, _) ;
      (
         SetIpAccessListLine_srcIpRange(list, line, srcIp_start, srcIp_end),
         Ip_address(srcIp:srcIpNum),
         srcIp_start <= srcIpNum <= srcIp_end
      )
   ).

IpAccessListMatchSrcPort(list, line, flow) <-
   IpAccessListLine(list, line),
   Flow_srcPort[flow] = srcPort,
   (
      !SetIpAccessListLine_srcPortRange(list, line, _, _) ;
      (
         SetIpAccessListLine_srcPortRange(list, line, srcPort_start, srcPort_end),
         srcPort_start <= srcPort <= srcPort_end
      )
   ).

// END CODE
   })
} <-- .
